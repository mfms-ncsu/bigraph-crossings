#!/bin/csh -f
#: summary2ci - script to produce 95% confidence intervals for data
#:              in SUMMARY files in specified class directories; uses
#:              confidence.awk 
#: Usage: confidence_intervals <class name>
#:        where <class name> is a string specifying a set of subdirectories
#:        of the data directory (enclosed in single or double quotes to
#:        prevent globbing)

if( "`setenv | fgrep PROCESSED_DIRECTORY`" == "" ) then
     set PROCESSED_DIRECTORY = $cwd/../processed
endif

if( $#argv != 1 ) then
    echo "Usage: $0 <class name>"
    echo "<class name> is a string specifying a set of subdirectories"
    echo "of the data directory (enclosed in single or double quotes to"
    echo "prevent globbing)"
    exit( 1 )
endif

set script_directory = $cwd
cd $PROCESSED_DIRECTORY
set file_list = `glob SUMMARY_$1`
foreach file ( $file_list )
    set class = `echo $file | cut -d_ -f 2-`
    echo "Generating 95% confidence intervals from summary for $class"
    if ( ! -r $file ) then
        echo "Unable to open $file for reading. Skipping..."
        continue
    endif
    set confidence_file = CONFIDENCE_$class
    if( -e $confidence_file ) then
        echo -n "Warning: $confidence_file already exists. Delete it? "
        if ( $< != "y" ) continue
        rm $confidence_file
    endif
    awk -f $script_directory/confidence.awk $file > $confidence_file
end
