#! /bin/tcsh -f
#: tree_ratios - creates a data file suitable for DeltaGraph, plotting
#:               ratios of crossing numbers between two treatments for
#:               trees of various dimensions
# Matt Stallmann, 22 Mar 2000

if( "`setenv | fgrep PROCESSED_DIRECTORY`" == "" ) then
     set PROCESSED_DIRECTORY = $cwd/../processed
endif

if( $#argv < 2 || $#argv > 3 ) then
    echo "Usage: $0 <treatment 1> <treatment 2> [ min ]"
    echo "to get ratio of mean crossings for treatment 1 vs. treatment 2"
    echo "(the third argument is included if min instead of mean is to be"
    echo " used - also implies scr class, and mean for treatment 1 if both"
    echo " treatments are the same)"
    exit( 1 )
endif

set tr1 = $1
set tr2 = $2
set field1 = 3
set field2 = 3
set suffix =
if( $#argv == 3 ) then
    set suffix = _scr
    set field2 = 2
    if( $tr1 != $tr2 ) then
        set field1 = 2
    endif
endif

set output_file = $PROCESSED_DIRECTORY/GT_${tr1}_${tr2}$suffix 

foreach dimension ( 1 1p001 1p01 1p1 2 0 )
    fgrep $tr1 \
        $PROCESSED_DIRECTORY/SUMMARY_GB_1_rnd${dimension}_0[1-8]$suffix \
        | cut -f $field1 > /tmp/tr1_$$
    fgrep $tr2 \
        $PROCESSED_DIRECTORY/SUMMARY_GB_1_rnd${dimension}_0[1-8]$suffix \
        | cut -f $field2 > /tmp/tr2_$$
    paste /tmp/tr1_$$ /tmp/tr2_$$ | awk -f quotient.awk \
        > /tmp/${dimension}_$$
    rm /tmp/tr?_$$
end

echo edges ':d = 1.0' ':d = 1.001' ':d = 1.01' ':d = 1.1' ':d = 2.0' \
    ':d = infinity' \
    | awk -F: '{ printf "%s\t%s\t%s\t%s\t%s\t%s\n",$1,$2,$3,$4,$5,$6; }' \
    > $output_file

paste edges_x /tmp/[1-2]*_$$ /tmp/0_$$ >> $output_file
rm /tmp/[0-2]*_$$

