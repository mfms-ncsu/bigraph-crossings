#!/bin/csh -f
#: gen_iso - script to create isomorphism classes from representatives of a
#:           specified collection of random classes 
#: Usage: gen_iso <class name> [ <individual> ]
#:        where <class name> is a string specifying a set of subdirectories
#:        of the data directory (enclosed in single or double quotes to
#:        prevent globbing) and <individual> is an integer specifying the
#:        particular representative of a class to use (default = 1); the
#:        class name of the new iso class has a suffix _scr<individual>

set class_size = 32

if( "`setenv | fgrep SEED_FILE`" == "" ) then
     set SEED_FILE = $cwd/seed_file
endif
if( "`setenv | fgrep DATA_DIRECTORY`" == "" ) then
     set DATA_DIRECTORY = $cwd/../data
endif
if( "`setenv | fgrep PROGRAM_DIRECTORY`" != "" ) then
     set path = ( ${PROGRAM_DIRECTORY} $path )
endif

if( $#argv < 1 || $#argv > 2 ) then
    echo "Usage: $0 <class name> [<individual>]"
    echo "<class name> is a string specifying a set of subdirectories"
    echo "of the data directory (enclosed in single or double quotes to"
    echo "prevent globbing)"
    echo "<individual> is an integer specifying the particular"
    echo "representative of a class to use (default = 1);"
    echo "the class name has a suffix _scr<individual>"
    exit( 1 )
endif

set individual = 0001
set suffix = _scr
if ( $#argv == 2 ) then
    set individual = $2
    set suffix = _scr${individual}
endif
cd $DATA_DIRECTORY
set class_list = `glob $1`
foreach dir ( $class_list )
    set name = ${dir}${suffix}
    set representative = ${dir}_${individual}.dot
    echo "Creating $name ..."
    mkdir $name
    cp $dir/$representative $name
    cd $name
    sbg_scramble $representative $class_size $SEED_FILE 0 1
    rm $representative
    cd ..
end

#  [Last modified: 2011 08 16 at 20:32:26 GMT]
