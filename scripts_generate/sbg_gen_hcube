#!/bin/csh -f
#: gen_hcube - create hypercubes only

# to change maximum rank, edit the foreach statement below
set class_size = 32

if( "`setenv | fgrep SEED_FILE`" == "" ) then
    set SEED_FILE = $cwd/seed_file
endif
if( "`setenv | fgrep DATA_DIRECTORY`" == "" ) then
    set DATA_DIRECTORY = $cwd/../data
endif
if( "`setenv | fgrep PROGRAM_DIRECTORY`" != "" ) then
    set path = ( ${PROGRAM_DIRECTORY} $path )
endif

# generate hypercubes with 2^k nodes where k is the rank
@ size = 4
foreach rank (02 03 04 05 06 07 08 09)
    set name = G_h_$rank
    echo "Creating directory $name"
    if( -e $DATA_DIRECTORY/$name ) then
        echo -n "Directory $name exists; remove it? "
        set answer = $<
        if( $answer == y ) then
            rm -rf $DATA_DIRECTORY/$name
        else
            continue
        endif
    endif
    mkdir $DATA_DIRECTORY/$name
    cd $DATA_DIRECTORY/$name
    sbg_comm_graph $size hcube $name
    sbg_scramble $name.dot $class_size $SEED_FILE 0 1
    rm $name.dot
    @ size = 2 * $size
end

#  [Last modified: 2011 08 16 at 20:32:26 GMT]
