#! /bin/csh -f
##: tr23 - a new treatment that alternates between barycenter and adaptive
#:        insertion (verbose version)
#: Usage: tr23 dotfile initial_ord_file
# Matt Stallmann, 21 Oct 1999

if( "`setenv | fgrep PROGRAM_DIRECTORY`" != "" ) then
     set path = ( ${PROGRAM_DIRECTORY} $path )
endif

set itr = 1024         # number of iterations of adaptive insertion
set dot_file = $1
set initial_ord = $2
set base_name = `basename $1 .dot`
set itr_file = ${base_name}_tr0023.itr
rm -f $itr_file
touch $itr_file

sbg_treat -tr=14 $dot_file $initial_ord
set new_ord = ${base_name}_tr0014.ord 
set new_crossings = `sbg_cn_eval $dot_file $new_ord | cut -f 2`
@ crossings = $new_crossings + 1 # guarantee at least one loop iteration 
while( $new_crossings < $crossings )
    @ crossings = $new_crossings
    set ord_file = $new_ord
    sbg_iterate -tr=03 -iterations=2048 $dot_file $ord_file
    echo "--- Barycenter ---" >> $itr_file
    cat ${base_name}_tr0003.itr >> $itr_file
    set new_ord = ${base_name}_tr0003.ord
    sbg_iterate -tr=04 -iterations=$itr $dot_file $new_ord
    echo "*** Adaptive Insertion ***" >> $itr_file
    cat ${base_name}_tr0004.itr >> $itr_file
    set new_ord = ${base_name}_tr0004.ord
    set new_crossings = `sbg_cn_eval $dot_file $new_ord | cut -f 2`
    rm -f ${base_name}_tr0014.ord
end
mv $ord_file ${base_name}_tr0023.ord
rm -f ${base_name}_tr000[34].ord ${base_name}_tr000[34].itr
echo ${base_name}_tr0023 $crossings | awk '{ printf "%s\t%d\n", $1, $2; }'
