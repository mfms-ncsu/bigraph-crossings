#! /bin/csh -f
##: tr23 - a new treatment that alternates between barycenter and adaptive
#:        insertion
#: Usage: tr23 dotfile initial_ord_file
# Matt Stallmann, 21 Oct 1999

if( "`setenv | fgrep PROGRAM_DIRECTORY`" != "" ) then
     set path = ( ${PROGRAM_DIRECTORY} $path )
endif

set itr = 1024         # number of iterations of adaptive insertion
set dot_file = $1
set initial_ord = $2
set base_name = `basename $1 .dot`

sbg_treat -tr=14 $dot_file $initial_ord
set new_ord = ${base_name}_tr0014.ord 
set new_crossings = `sbg_cn_eval $dot_file $new_ord | cut -f 2`
@ crossings = $new_crossings + 1 # guarantee at least one loop iteration 
while( $new_crossings < $crossings )
    @ crossings = $new_crossings
    set ord_file = $new_ord
    sbg_treat -tr=03 -iterations=2048 $dot_file $ord_file
    set new_ord = ${base_name}_tr0003.ord
    sbg_treat -tr=04 -iterations=$itr $dot_file $new_ord
    set new_ord = ${base_name}_tr0004.ord
    set new_crossings = `sbg_cn_eval $dot_file $new_ord | cut -f 2`
end
mv $ord_file ${base_name}_tr0023.ord
rm ${base_name}_tr00[01][34].ord
echo ${base_name}_tr0023 $crossings | awk '{ printf "%s\t%d\n", $1, $2; }'
