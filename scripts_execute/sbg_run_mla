#!/bin/csh -f
##: run_mla - script to run the minimum linear arrangement algorithm on a class.
#
# Usage: run_mla <var>class_name</var> [<var>number_of_subjects</var>]
#
#        <var>class_name</var> is a string specifying a set of subdirectories
#        of the data directory (enclosed in single or double quotes to
#        prevent globbing)

if( "`setenv | fgrep DATA_DIRECTORY`" == "" ) then
     set DATA_DIRECTORY = $cwd/../data
endif
if( "`setenv | fgrep PROGRAM_DIRECTORY`" != "" ) then
     set path = ( ${PROGRAM_DIRECTORY} $path )
endif

if( $#argv < 1 || $#argv > 2 ) then
    echo "Usage: $0 <class name> [<# of subjects>]"
    echo "<class name> is a string specifying a set of subdirectories"
    echo "of the data directory (enclosed in single or double quotes to"
    echo "prevent globbing)"
    exit( 1 )
endif

cd $DATA_DIRECTORY
set class_list = `glob $1`
set treatment = 40
if ( $#argv == 2 ) then
    @ subjects = $2
    set results = "EXP_TR${treatment}_$subjects"
else
    @ subjects = 64    # or a suitably large number
    set results = "EXP_TR${treatment}"
endif
foreach dir ( $class_list )
    cd $DATA_DIRECTORY/$dir
    if( -e $results ) mv $results $results.bak
    touch $results
    if( -e MLA ) mv MLA MLA.bak
    touch MLA
    @ count_subjects = 0
    foreach file ( *.dot )
        set base = `basename $file .dot`
        sbg_mla $file >> MLA
        sbg_cn_eval $file ${base}_tr00$treatment.ord >> $results
        rm ${base}_tr00$treatment.ord
        @ count_subjects = $count_subjects + 1
        if ( $count_subjects >= $subjects ) break
    end
end
